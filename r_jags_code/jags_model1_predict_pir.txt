#model with interactions between the social variables and the ecological variables
model {

#fixed-effects and random-effects at the BRRep level - these are ecological community effects
for (i in 1:NUMBR)
{
	#BRRep level ecological effects
	a[i] ~ dnorm(ma[i],ta);
	#BRRep level linear predictor (fixed effects)
	ma[i] <- alpha + nst * NST[i];
	
	#BRRep level pir, pid random-effects
	pir[i] ~ dnorm(mpir[i],tpir);
	pid[i] ~ dnorm(mpid[i],tpid);
	
	#interactions between ecological community characteristics and pir and pid	
	mpir[i] <- alpir + nspir * NST[i];
	mpid[i] <- alpid + nspid * NST[i];	
}

for (i in 1:N)
{
	#true expected values for SC 
	TrueSC[i] ~ dnorm(mSC[i],tau);
	#linear predictors - these are social network interaction effects
	mSC[i] <- a[BR[i]] + pir[BR[i]] * PIR[i] + pid[BR[i]] * PID[i];
			
	#observation model
	#observed mean quantities with error due to simulations within each BR replicate
	SC[i] ~ dnorm(TrueSC[i],TAUSC[i]);

	#goodness-of-fit & model adequacy (uncomment to use)
	#compute discrepancy statistics for observed data
	#res.obs[i] <- SC[i] - mSC[i];
	#R.obs[i] <- pow((SC[i] - mSC[i]),2) / mSC[i];
	#compute discrepancy statistics for simulated data
	#TSCSim[i] ~ dnorm(mSC[i],tau);
	#SCSim[i] ~ dnorm(TSCSim[i],TAUSC[i]);
	#res.sim[i] <- SCSim[i] - mSC[i];
	#R.sim[i] <- pow((SCSim[i] - mSC[i]),2) / mSC[i];	
}

#calculate goodness-of-fit statistics (uncomment to use)
#fit.obs <- sum(R.obs[]);
#fit.sim <- sum(R.sim[]);
#fit.test <- fit.obs - fit.sim;

#make predictions of expected values for the range of NST and PIR values specified
for (i in 1:NUMPN)
{
	for (j in 1:NUMPIR)
	{
		pred[i,j] <- alpha + nst * NSTPRED[i] + alpir * PIRPRED[j] + nspir * NSTPRED[i] * PIRPRED[j];
	}
}
	
#priors
alpha ~ dnorm(0,0.001);
nst ~ dnorm(0,0.001);
ta <- sa^-2;
sa ~ dlnorm(0,0.001);
alpir ~ dnorm(0,0.001);
nspir ~ dnorm(0,0.001);
tpir <- spir^-2;
spir ~ dlnorm(0,0.001);
alpid ~ dnorm(0,0.001);
nspid ~ dnorm(0,0.001);
tpid <- spid^-2;
spid ~ dlnorm(0,0.001);	
tau <- sd^-2;
sd ~ dlnorm(0,0.001);

}
