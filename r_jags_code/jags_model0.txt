#model without interactions
model {

#fixed-effects and random-effects at the BRRep level - these are ecological community effects
for (i in 1:NUMBR)
{
	#BRRep level linear predictor (fixed effects)
	a[i] <- alpha + nst * NST[i];
	
	#BRRep level pir, pid, pird random-effects
	pir[i] ~ dnorm(mpir,tpir);
	pid[i] ~ dnorm(mpid,tpid);
}

for (i in 1:N)
{
	#true expected values for SC 
	TrueSC[i] ~ dnorm(mSC[i],tau);
	#linear predictors
	mSC[i] <- a[BR[i]] + pir[BR[i]] * PIR[i] + pid[BR[i]] * PID[i];
			
	#observation model
	#observed mean quantities with error due to simulations within each BR replicate
	SC[i] ~ dnorm(TrueSC[i],TAUSC[i]);	
}	
	
#priors
alpha ~ dnorm(0,0.001);
sp ~ dnorm(0,0.001);
occ ~ dnorm(0,0.001);
nst ~ dnorm(0,0.001);
mpir ~ dnorm(0,0.001);
tpir <- spir^-2;
spir ~  dlnorm(0,0.001);
mpid ~ dnorm(0,0.001);
tpid <- spid^-2;
spid ~  dlnorm(0,0.001);	
tau <- sd^-2;
sd ~  dlnorm(0,0.001);

}
